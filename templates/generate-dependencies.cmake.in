message("       Including ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-link-libraries.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-link-libraries.cmake")
if(NOT DEFINED imported_libraries)
    message(FATAL_ERROR "imported_libraries variable is not defined.")
endif()
if(NOT DEFINED buildsystem_targets)
    message(FATAL_ERROR "buildsystem_targets variable is not defined.")
endif()

message("       Export component '@component@' contains targets: ${targets}")
message("           imported_libraries for targets: ${imported_libraries}")
message("           (all buildsystem_targets: ${buildsystem_targets})")

message("       Reading package dependencies JSON ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-package-dependencies.json")
file(READ ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-package-dependencies.json package_dependencies_json)
string(JSON package_dependencies GET "${package_dependencies_json}" "package_dependencies")
string(JSON num_deps LENGTH "${package_dependencies}")
math(EXPR max_idx "${num_deps} - 1")

set(fd "")
foreach(imported_lib IN LISTS imported_libraries)
    if(NOT imported_lib)
        continue()
    endif()

    message("       Processing imported library: ${imported_lib}")

    if(${imported_lib} IN_LIST buildsystem_targets)
        message("           Skipping imported library '${imported_lib}' as it is part of the buildsystem targets.")
        continue()
    endif()

    # Find the corresponding package dependency JSON info
    set(package_json "")
    foreach(idx RANGE ${max_idx})
        string(JSON dep_i_json GET "${package_dependencies}" ${idx})
        string(JSON package_targets GET "${dep_i_json}" "package_targets")
        if(${imported_lib} IN_LIST package_targets)
            set(package_json ${dep_i_json})
            break()
        endif()
    endforeach()

    if(NOT package_json)
        message(WARNING "Could not find package dependency information for imported target: ${imported_lib}")
        continue()
    endif()

    # Parse the package JSON info
    string(JSON package_name GET "${package_json}" "package_name")
    string(JSON find_package_args GET "${package_json}" "find_package_args")
    string(JSON package_targets GET "${package_json}" "find_package_args")
    string(JSON module_file GET "${package_json}" "module_file")

    if(NOT fd)
        set(fd "include(CMakeFindDependencyMacro)\n\n")
    endif()

    # Handling if package is using a custom module
    if(module_file)
        string(APPEND fd "list(APPEND CMAKE_MODULE_PATH \${CMAKE_CURRENT_LIST_DIR}/find-modules/${package_name})\n\n")

        # New Method: install the files directly
        file(INSTALL DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/cmake/@PROJECT_NAME@/find-modules/${package_name} TYPE FILE FILES ${module_file})
    endif()

    string(REPLACE ";" " " find_package_args "${find_package_args}")

    # Handling find_dependency logic
    if(NOT package_targets)
        string(APPEND fd "find_dependency(${find_package_args})\n")
    else()
        string(APPEND fd "if(NOT TARGET ${imported_lib})\n")
        string(APPEND fd "    message(\"            ==> Executing find_dependency(${find_package_args})\")\n")
        string(APPEND fd "    find_dependency(${find_package_args})\n")
        string(APPEND fd "else()\n")
        string(APPEND fd "    message(\"            ==> Target ${imported_lib} already exists, skipping find_dependency(${find_package_args})\")\n")
        string(APPEND fd "endif()\n\n")
    endif()
endforeach()

string(PREPEND fd "# Generated file - do not edit
# This file contains the generated dependencies for component '@component@'\n")

# Read existing file and compare
if(EXISTS ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-dependencies.cmake)
    file(READ ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-dependencies.cmake existing_fd)
endif()

if(fd STREQUAL existing_fd)
    message("       No changes detected in dependencies for component '@component@', skipping generation.")
else()
    message("       Generating ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-dependencies.cmake")
    file(WRITE ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-dependencies.cmake "${fd}")
endif()

# New Method: install the files directly
file(INSTALL DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/cmake/@PROJECT_NAME@ TYPE FILE FILES ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-@component@-dependencies.cmake)
