# Generated by CMake @CMAKE_VERSION@ for @PROJECT_NAME@
@PACKAGE_INIT@
message("Welcome to @PROJECT_NAME@ !")

# valid components that can be searched for
set(@PROJECT_NAME@_supported_components @xxx_project_components@)
message("   Valid components for @PROJECT_NAME@: ${@PROJECT_NAME@_supported_components}")
message("   User is requesting components: ${@PROJECT_NAME@_FIND_COMPONENTS}")

# iterate through components attempting to be found
foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})
    message("       Checking component ${component}")

    if(NOT ${component} IN_LIST @PROJECT_NAME@_supported_components)
        set(@PROJECT_NAME@_FOUND False)
        string(REPLACE ";" ", " @PROJECT_NAME@_components_pp "${@PROJECT_NAME@_supported_components}")
        set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Unsupported component '${component}'.\n Supported components are:\n ${@PROJECT_NAME@_components_pp}")
        break()
    else()
        message("           Including ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-${component}-targets.cmake")
        include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-${component}-targets.cmake")

        message("           Including ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-${component}-dependencies.cmake")
        include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-component-${component}-dependencies.cmake")

        set(@PROJECT_NAME@_${component}_FOUND True)
    endif()
endforeach()
